****************************************************************
*
*   Ausstehend:
*
* - Itabs direkt je nach verfügbaren Parametern befüllen
* - Suche nach Merkmal, Status, Beschreibung
* - Erweiterte Programmprüfung
* - Spaltennamen des ALV-Grid anpassen
*
****************************************************************
REPORT zdocinfo.
****************************************************************
* Parameter und Daten
****************************************************************
TABLES: draw, drat, rctms.

SELECTION-SCREEN BEGIN OF BLOCK bl0 WITH FRAME TITLE TEXT-000.
  SELECT-OPTIONS: pa_dokar FOR draw-dokar NO INTERVALS OBLIGATORY,
                  pa_doknr FOR draw-doknr,
                  pa_dokvr FOR draw-dokvr,
                  pa_doktl FOR draw-doktl,
                  pa_dokst FOR draw-dokst.
SELECTION-SCREEN END OF BLOCK bl0.

SELECTION-SCREEN BEGIN OF BLOCK bl1 WITH FRAME TITLE TEXT-003.
  SELECT-OPTIONS: so_mmal FOR rctms-mname,
                  so_bsbg FOR drat-dktxt.
SELECTION-SCREEN END OF BLOCK bl1.

*SELECTION-SCREEN BEGIN OF BLOCK bl1 WITH FRAME TITLE TEXT-005.
*  SELECT-OPTIONS:
*    stdoknr FOR draw-doknr NO INTERVALS.
*
** define push-button "Search" for WEB-EWT's
*  SELECTION-SCREEN:
*  PUSHBUTTON pos_high(12) pb_sel USER-COMMAND sel MODIF ID 002.
*
*  SELECT-OPTIONS:
*    stdokar FOR draw-dokar NO INTERVALS.
*
** define push-button "Reset" for WEB-EWT's
*  SELECTION-SCREEN:
*  PUSHBUTTON pos_high(12) pb_res USER-COMMAND vres MODIF ID 002.
*
*  " Start : Customer Connect for Dynamic Search Improvements: D7997
*  PARAMETERS: p_frees  TYPE rsds_type  NO-DISPLAY .
*  PARAMETERS  : pa_num      LIKE sy-tfill NO-DISPLAY.
** Where-Clause for free Selection
*  DATA:  gt_item_tab_clauses TYPE rsds_twhere.
*  DATA:  gt_ranges           TYPE rsds_trange.
*  DATA:  gt_fields           TYPE rsdsfields_t.
*  DATA:  gt_field_names      TYPE TABLE OF rsdstexts." wzre_rsdstexts_tab.
*  DATA : l_buttxt(30) TYPE c .
*  " End : Customer Connect for Dynamic Search Improvements: D7997
*
*  SELECT-OPTIONS:
*    stdoktl FOR draw-doktl NO INTERVALS,
*    stdokvr FOR draw-dokvr NO INTERVALS.
*  PARAMETERS restrict TYPE anzhl_s."Note 948133
*SELECTION-SCREEN END OF BLOCK bl1.

DATA: it_tab  TYPE STANDARD TABLE OF draw,
      it_tab2 TYPE STANDARD TABLE OF drad.
****************************************************************
* Event-Klasse
****************************************************************
CLASS lcl_events DEFINITION FINAL.

  PUBLIC SECTION.
    CLASS-METHODS: on_double_click
      FOR EVENT if_salv_events_actions_table~double_click
      OF cl_salv_events_table
      IMPORTING row,
      on_link_click
        FOR EVENT if_salv_events_actions_table~link_click
        OF cl_salv_events_table.

ENDCLASS.

CLASS lcl_events IMPLEMENTATION.
  METHOD on_double_click.
    DATA: ls_draw TYPE draw.
    READ TABLE it_tab INTO ls_draw INDEX row. "Speichert die angeklickte Zeile in der Struktur
    SET PARAMETER ID 'CV1' FIELD ls_draw-doknr.
    SET PARAMETER ID 'CV2' FIELD ls_draw-dokar.
    SET PARAMETER ID 'CV3' FIELD ls_draw-dokvr.
    SET PARAMETER ID 'CV4' FIELD ls_draw-doktl.
    CALL TRANSACTION 'CV03N' WITHOUT AUTHORITY-CHECK.
  ENDMETHOD.

  METHOD on_link_click.
    TRY.
        DATA: o_salv TYPE REF TO cl_salv_table.
        CALL METHOD cl_salv_table=>factory(
          IMPORTING
            r_salv_table = o_salv
          CHANGING
            t_table      = it_tab2 ).

        o_salv->get_functions(  )->set_all( abap_true ).
        o_salv->get_display_settings(  )->set_list_header( TEXT-001 ).
        o_salv->get_display_settings(  )->set_striped_pattern( abap_true ).
        o_salv->get_selections(  )->set_selection_mode( if_salv_c_selection_mode=>row_column ).
        o_salv->get_sorts(  )->add_sort( columnname = 'DOKVR' sequence = if_salv_c_sort=>sort_up ).
        o_salv->get_columns(  )->set_optimize( abap_true ).
        zvz_cl_salv=>hide_empty_columns( o_salv = o_salv it_tab = it_tab2 ).
        LOOP AT o_salv->get_columns(  )->get(  ) ASSIGNING FIELD-SYMBOL(<c>).
          DATA(o_col2) = <c>-r_column.
          o_col2->set_alignment( if_salv_c_alignment=>centered ).
        ENDLOOP.
        o_salv->display(  ).
      CATCH cx_root INTO DATA(e_txt).
        MESSAGE e_txt TYPE 'E'.
    ENDTRY.
  ENDMETHOD.

ENDCLASS.

START-OF-SELECTION.
****************************************************************
* Füllt itab
****************************************************************
  SELECT *
  FROM draw
  WHERE dokar IN @pa_dokar
  INTO TABLE @it_tab.

  IF pa_doknr IS NOT INITIAL.
    DELETE it_tab WHERE doknr NOT IN pa_doknr.
  ENDIF.

  IF pa_dokvr IS NOT INITIAL.
    DELETE it_tab WHERE dokvr NOT IN pa_dokvr.
  ENDIF.

  IF pa_doktl IS NOT INITIAL.
    DELETE it_tab WHERE doktl NOT IN pa_doktl.
  ENDIF.

  IF pa_dokst IS NOT INITIAL.
    DELETE it_tab WHERE dokst NOT IN pa_dokst.
  ENDIF.
****************************************************************
* Füllt itab2
****************************************************************
  SELECT *
  FROM drad
  WHERE dokar IN @pa_dokar
  INTO TABLE @it_tab2.

  IF pa_doknr IS NOT INITIAL.
    DELETE it_tab2 WHERE doknr NOT IN pa_doknr.
  ENDIF.

  IF pa_dokvr IS NOT INITIAL.
    DELETE it_tab2 WHERE dokvr NOT IN pa_dokvr.
  ENDIF.

  IF pa_doktl IS NOT INITIAL.
    DELETE it_tab2 WHERE doktl NOT IN pa_doktl.
  ENDIF.
****************************************************************
* ALV
****************************************************************
  TRY.
      DATA: o_salv   TYPE REF TO cl_salv_table,
            o_events TYPE REF TO cl_salv_events_table.
      CALL METHOD cl_salv_table=>factory(
        IMPORTING
          r_salv_table = o_salv
        CHANGING
          t_table      = it_tab ).
****************************************************************
* ALV-Spalten ausblenden
****************************************************************
      zvz_cl_salv=>hide_column( o_salv = o_salv lv_colnam = 'MANDT' ).
      zvz_cl_salv=>hide_empty_columns( o_salv = o_salv it_tab = it_tab ).
****************************************************************
* ALV-Header
****************************************************************
      DATA(o_grid_header) = NEW cl_salv_form_layout_grid(  ).
      o_grid_header->create_flow( row = 5 column = 1 )->create_text( text = '' ).
      o_grid_header->create_flow( row = 6 column = 1 )->create_text( text = 'Navigation:' ).
      o_grid_header->create_flow( row = 6 column = 2 )->create_text( text = 'Zeile doppelklicken, um Dokumentinfosatz anzuzeigen' ).
      o_grid_header->create_flow( row = 7 column = 2 )->create_text( text = 'Dokumentart anklicken, um Objektverknüpfungen anzuzeigen' ).
      o_salv->set_top_of_list( o_grid_header ).
****************************************************************
* ALV-Events
****************************************************************
      o_events = o_salv->get_event(  ).
      SET HANDLER lcl_events=>on_double_click FOR o_events.
      SET HANDLER lcl_events=>on_link_click FOR o_events.
      DATA(o_col) = CAST cl_salv_column_table( o_salv->get_columns(  )->get_column( 'DOKAR' ) ).
      o_col->set_cell_type( if_salv_c_cell_type=>hotspot ).
****************************************************************
* ALV-Ausgabe
****************************************************************
      o_salv->get_functions(  )->set_all( abap_true ).
      o_salv->get_display_settings(  )->set_list_header( TEXT-002 ).
      o_salv->get_display_settings(  )->set_striped_pattern( abap_true ).
      o_salv->get_selections(  )->set_selection_mode( if_salv_c_selection_mode=>row_column ).
      o_salv->get_sorts(  )->add_sort( columnname = 'DOKNR' sequence = if_salv_c_sort=>sort_up ).
      o_salv->get_columns(  )->set_optimize( abap_true ).
      LOOP AT o_salv->get_columns(  )->get(  ) ASSIGNING FIELD-SYMBOL(<c>).
        DATA(o_col2) = <c>-r_column.
        o_col2->set_alignment( if_salv_c_alignment=>centered ).
      ENDLOOP.
      o_salv->display(  ).
    CATCH cx_root INTO DATA(e_txt).
      WRITE: / e_txt->get_text(  ).
  ENDTRY.